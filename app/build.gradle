plugins {
    id("com.android.application")
    id("io.objectbox") // Apply last.
}

def mapProperties = new Properties()
try {
    mapProperties.load(new FileInputStream(rootProject.file("map.properties")))
}
catch (FileNotFoundException ignored) {
    mapProperties['google_maps_api_key'] = 'MISSING'
}

try {
    mapProperties['google_creds'] = new File('credentials.json').text
}
catch (FileNotFoundException ignored) {
    mapProperties['google_creds'] = 'MISSING'
}

android {
    defaultConfig {
        applicationId "com.example.clicker"
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34
        versionCode 2
        versionName String.format("%s  %s", "3.0", new Date().format('yyyy-MM-dd'))
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [mapApiKey: mapProperties['google_maps_api_key'], googleCreds: mapProperties['google_creds'], weatherKey: mapProperties['weather.key']]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES']
        }
    }
    namespace 'com.example.clicker'
    signingConfigs {
        debug {
            storeFile file('debug-dbuss.keystore')
            keyAlias "androiddebugkey"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.2'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.shredzone.commons:commons-suncalc:3.11'
    implementation 'org.locationtech.proj4j:proj4j:1.3.0'
    implementation 'com.google.api-client:google-api-client:2.6.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.36.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20230227-2.0.0'
    implementation 'com.github.50ButtonsEach:flic2lib-android:1.3.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'org.json:json:20240303'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    // Fix Duplicate class
    //implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
}
